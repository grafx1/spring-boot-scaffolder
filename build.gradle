plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'application'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"

}

group = 'io.github.grafx1.scaffolder'
version = '2.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.freemarker:freemarker:2.3.32'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

application {
    mainClass = 'io.github.grafx1.scaffolder.Main'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Spring Boot Scaffolder',
                'Implementation-Version': version,
                'Main-Class': application.mainClass.get()
        )
    }
}

gradlePlugin {
    website = 'https://github.com/grafx1/spring-boot-scaffolder'
    vcsUrl = 'https://github.com/grafx1/spring-boot-scaffolder.git'
    plugins {
        scaffoldPlugin {
            id = 'io.github.grafx1.scaffolder'
            implementationClass = 'io.github.grafx1.scaffolder.ScaffolderPlugin'
            displayName = 'Spring Boot scaffolding Plugin'
            description = 'Spring Boot scaffolding generator based on FreeMarker : Entity, DTO, Service, Controller...'
            tags.set(['spring', 'freemarker', 'scaffolding', 'spring-boot', 'boilerplate'])
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'spring-boot-scaffolder'
            version = project.version

            pom {
                name = 'Spring Boot Scaffolder Plugin'
                description = 'Generate layered architecture for Spring Boot (Entity, Mapper, Repository, DTO, Service, Controller)'
                url = 'https://github.com/grafx1/spring-boot-scaffolder'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'grafx1'
                        name = 'Moctar DIOP'
                        email = 'moctar.diop@live.fr'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/grafx1/spring-boot-scaffolder.git'
                    developerConnection = 'scm:git:ssh://github.com:grafx1/spring-boot-scaffolder.git'
                    url = 'https://github.com/grafx1/spring-boot-scaffolder'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}


signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty("ossrhUsername")
            password = findProperty("ossrhPassword")
        }
    }
}

// Corrige les dépendances entre les tâches de signature et de publication
afterEvaluate {
    def signTasks = tasks.withType(Sign)
    def publishTasks = tasks.matching { it.name.startsWith("publish") }

    publishTasks.each { publishTask ->
        signTasks.each { signTask ->
            // Ajoute une dépendance explicite seulement si les tâches partagent des artefacts
            publishTask.dependsOn(signTask)
        }
    }
}